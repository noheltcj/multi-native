buildscript {
    repositories {
        mavenCentral()
        maven { url "https://dl.bintray.com/jetbrains/kotlin-native-dependencies" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:${project.property('konan.plugin.version')}"
    }
}

apply plugin: 'konan'

def frameworkName = 'MultiNative'

konanArtifacts {
    framework(frameworkName, targets: ['ios_arm64', 'ios_x64' ]) {
        enableMultiplatform true
    }
}

def outputDirectory = "build/bin/${project.version}"
def outputFramework = "$outputDirectory/${frameworkName}.framework"

task combineArchitectures(type: Exec, dependsOn: compileKonanMultiNative) {
    executable 'lipo'
    args = ['-create', '-arch', 'arm64',
            new File(compileKonanMultiNativeIos_arm64.artifact, frameworkName),
            '-arch', 'x86_64',
            new File(compileKonanMultiNativeIos_x64.artifact, frameworkName),
            '-output', "$outputFramework/$frameworkName"
    ]
}

task exportFramework(type: Copy, dependsOn: compileKonanMultiNative) {
    from compileKonanMultiNativeIos_arm64.artifact
    into outputFramework
    exclude frameworkName
    finalizedBy combineArchitectures
}

dependencies {
    expectedBy project(':multi-native-common')
}
